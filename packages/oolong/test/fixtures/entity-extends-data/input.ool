type
  password : text maxLength(200)
  email : text maxLength(200) ~isEmail    
  phone : text maxLength(20) ~matches(/^((\+|00)\d+)?\d+(-\d+)?$/)
  userStatus : enum values(['inactive', 'active', 'disabled', 'forbidden', 'deleted'])
  name : text maxLength(20)

entity user
  -- 'User Credential';

  with
    autoId({ startFrom: 100000 })
    createTimestamp
    updateTimestamp
    logicalDeletion({ status: 'deleted' })
    stateTracking("status")
    atLeastOneNotNull([ 'email', 'mobile' ])

  has
    email 
    mobile : phone ~isMobilePhone(@latest.locale |> stringDasherize) |> normalizeMobile    
    status : userStatus default('inactive')
    password |> hashPassword(@latest.passwordSalt)
    passwordSalt : text fixedLength(8) auto 

  associations  
    hasMany personalProfile 
    hasMany securityGroup

  index
    email unique
    mobile unique

entity personalProfile extends profile
  -- 'Personal Profile'
  with
    updateTimestamp
  has    
    firstName : name optional
    middleName : name optional
    surName : name optional
    dateOfBirth : datetime optional
  key owner
  associations
    belongsTo user as owner
    refersTo gender 
    refersTo personTitle


entity gender extends dictionary
  data {
    'M' : { name: 'male' },
    'F' : { name: 'female' },
    'U' : { name: 'unknown' }
  }

entity personTitle extends dictionary
  has
    maleCapable : bool default(true)
    femaleCapable : bool default(true)
  data {
    'MR' : { name: 'Mr', femaleCapable: false },
    'MRS' : { name: 'Mrs', maleCapable: false },
    'MS' : { name: 'Ms', maleCapable: false },
    'MISS' : { name: 'Miss', maleCapable: false },
    'DR': { name: 'Dr' }
  }